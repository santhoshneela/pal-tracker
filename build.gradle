import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id 'org.springframework.boot' version '2.4.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "org.flywaydb.flyway" version "7.3.0"
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    //mvc app
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //spring jdbc
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'

    //mysql connector
    implementation 'mysql:mysql-connector-java'

    //for tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

// for unit 5 tests
test {
    useJUnitPlatform()
}

//dev db url
def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
//Adding environment variable to local run
bootRun.environment([
        "WELCOME_MESSAGE": "howdy",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
])

//test db url
def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
//Adding environment variable to tests
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
])

flyway {
         url = developmentDbUrl
         user = "tracker"
         password = ""
         locations = ["filesystem:databases/tracker/migrations"]
}
task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}


